---
layout: post
title: "Codificação de Huffman: parte 1"
---

## Introdução

Você já se perguntou como um algoritmo de **_compressão de textos_** funciona? Nessa série de posts você vai aprender o funcionamento de um algoritmo clássico de compressão conhecido como **Código de Huffman**. Considerando a codificação ASCII, onde cada caractere ocupa 1 byte  (8 bits) de memória, um texto com 1000 caracteres (incluindo espaços e pontuação) ocuparia 1000 bytes. Sabemos, por outro lado, que alguns caracteres, dependendo da língua, ocorrem com mais frequência que outros. Em português brasileiro, por ex., a letra 'A' é a letra que mais aparece em textos segundo esse estudo[1]. Na verdade, as vogais correspondem a quase metade dos textos. 

Que ideia genial é essa, afinal? Sabendo que certas letras ocorrem com muito mais frequência que outras e sabendo que todas ocupam o mesmo espaço de memória, David Huffman (1925-1999) teve a brilhante ideia de representar as letras que ocorrem com mais frequência utilizando menos bits. Trocando em miúdos, **quanto mais uma letra aparece em um texto, menos bits deve-se usar para representá-la**. Desta forma, um texto com 1000 caracteres não vai, necessariamente, ocupar 1000 bytes, já que agora os caracteres com maior frequência no texto, ocuparão um espaço menor que 1 byte.

## O Algoritmo

# Criação de tabela de frequências

O primeiro passo é montar a tabela de frequências de cada caractere no texto. Nesse post eu vou usar a linguagem Clojure para exemplificar o código. Para nos ajudar nessa tarefa, a linguagem nos fornece a função ```frequencies``` que retorna um hash-map com as frequências dos caracteres.

```
> (frenquencies "O rato roeu a roupa do rei de roma.")
{\space 8, 
 \a 4, 
 \d 2, 
 \e 3, 
 \i 1, 
 \m 1, 
 \. 1, 
 \O 1, 
 \o 5, 
 \p 1, 
 \r 5, 
 \t 1, 
 \u 2}
```
Clojure representa char com uma \ na frente do caractere. Nesse exemplo vemos que o espaço(\space) apareceu 8 vezes nessa frase e a letra 'a'(\a) apareceu 4 vezes.

A partir dessa estrutura, vamos criar uma _fila de prioridades_ com os **nós** que representam o símbolo e sua frequência no texto. Em um segundo momento criaremos uma árvore a partir dessa fila. Para representar um nó, vamos utilizar um hash-map com a seguinte estrutura:
```
{:symbol "a"
 :freq 4
 :left nil
 :right nil}
```
Criei uma função chamada ```node```, sobrecarregada na sua aridade, que retorna um novo nó. 
{% highlight clojure %}
(defn node
  ([symbol
    freq
    left
    right]
   {:symbol symbol
    :freq freq
    :left left
    :right right})
  ([symbol
    freq]
   (node (str symbol) freq nil nil))
  ([entry]
   (node (first entry) (second entry))))
{% endhighlight %}

O nó, portanto, guarda um caractere, a sua frequência no texto e dois nós filhos, left e right, que começam com o valor nulo. 

Vamos imaginar a seguinte fila de nós:
![](/img/fila1.jpeg)

A estrutura da imagem acima em clojure se torna:
{% highlight clojure %}
[{:symbol "c", :freq 2, :left nil, :right nil} {:symbol "x", :freq 4, :left nil, :right nil} {:symbol "j", :freq 5, :left nil, :right nil} {:symbol "e", :freq 10, :left nil, :right nil} {:symbol "a", :freq 25, :left nil, :right nil}]
{% endhighlight %}

Para gerar essa fila de prioridade eu criei uma função chamada ```build-priority-queue``` que recebe um texto como entrada e gera a fila de nós. Abaixo segue o código da mesma:

{% highlight clojure %}
(defn build-priority-queue
  [text]
  (let [char-freq (frequencies text)
        nodes (map node char-freq)]
    (sort-by :freq nodes)))

{% endhighlight %}

A partir dessa fila construiremos a árvore trie que produzirá a nova codificação dos caracteres. O algoritmo segue da seguinte forma:

1. Enquanto o tamanho da fila for maior que 1, faça:
2. Remova os dois primeiros nós e faça o merge* dos mesmos
3. Reinsira esse novo nó na fila de prioridade
4. Continue até que sobre apenas 1 nó na fila, que será a raiz da árvore.

Um ponto importante que precisa ser mencionado é como fazer o merge de dois nós. Na imagem abaixo podemos ver o início do processo:

![](/img/fila2.jpeg)

A junção dos dois primeiros nós se dá da seguinte forma:
1. Crie um novo nó cuja frequência é a soma das frequências dos dois primeiros nós da fila.
2. O símbolo desse nó deve ser vazio.
3. Nesse novo nó, atribua o nó de menor frequência para seu filho à esquerda e atribua o nó de maior frequência ao seu filho à direita.

Na imagem abaixo podemos ver esse processo:

![](/img/merge1.jpeg)

Eu fiz uma função em clojure chamada ```merge-nodes``` que recebe dois nós ```node1 e node2```. 

{% highlight clojure %}
(defn merge-nodes
  [node1
   node2]
  (let [freq1 (:freq node1)
        freq2 (:freq node2)]
    (if (<= freq1 freq2)
      (node "" (+ freq1 freq2) node1 node2)
      (node "" (+ freq1 freq2) node2 node1))))
{% endhighlight %}

Se você leu meu post anterior sobre [Estruturas Algébricas](https://compdepressivo.github.io/math/algebra/programming/2018/09/16/algebra-para-programadores.html) vai perceber que essa função ```merge``` **é fechada para o conjunto dos nós**, consequentemente formando um **_SemiGrupo_**. 

De posse desse novo nó, insira-o novamente na fila de prioridade, ordenada pela frequência. Nesse caso, a fila resultante será:

![](/img/fila3.jpeg)

Continuando o processo temos que juntar novamente os dois primeiros nós da fila:

![](/img/merge2.jpeg)

O resultado desse merge será o nó:

![](/img/merge3.jpeg)

Vamos inserir esse novo nó na fila de prioridades e o resultado será:

![](/img/fila4.jpeg)

Como a fila ainda possui 3 elementos, devemos continuar o processo de merge:

![](/img/merge4.jpeg)

Como resultado, teremos a fila:

![](/img/fila5.jpeg)

Agora a fila possui apenas 2 elementos e faremos o processo de merge pela última vez, gerando a seguinte árvore:

![](/img/tree1.jpeg)

Você, como um leitor perspicaz, pode perceber que **os elementos que possuem maior frequência no texto, nesse caso E:10 e A:25, ficaram mais próximos da raiz, e elementos com pouca frequência, como C:2 e X:4, ficaram nos níveis mais baixos da árvore. Vamos entender o motivo para isso. Mas antes, vamos ver a função ```build-tree``` que recebe a fila de nós e gera a árvore resultante:

{% highlight clojure %}
(defn build-tree
  [priority-queue]
  (if (= 1 (count priority-queue))
    (first priority-queue)
    (let [node1 (first priority-queue)
          node2 (second priority-queue)
          node (merge-nodes node1 node2)]
      (recur (sort-by :freq (conj (drop 2 priority-queue) node))))))
{% endhighlight %}

Em clojure, essa árvore ficou com a seguinte cara:

{% highlight clojure %}
{:symbol "",
 :freq 46,
 :left
 {:symbol "",
  :freq 21,
  :left {:symbol "e", :freq 10, :left nil, :right nil},
  :right
  {:symbol "",
   :freq 11,
   :left {:symbol "j", :freq 5, :left nil, :right nil},
   :right
   {:symbol "",
    :freq 6,
    :left {:symbol "c", :freq 2, :left nil, :right nil},
    :right {:symbol "x", :freq 4, :left nil, :right nil}}}},
 :right {:symbol "a", :freq 25, :left nil, :right nil}}
{% endhighlight %}


Para gerar a nova codificação para os símbolos dessa árvore, vamos adotar a seguinte estratégia: sempre que caminharmos para a esquerda na árvore, vamos adicionar um 0 ao bitcode e sempre que caminharmos para a direita devemos adicionar 1 ao bitcode. Como resultado temos a seguinte codificação:

![](/img/fulltree.jpeg)

Como podemos perceber, o símbolo com maior frequência, A:25, foi o símbolo que recebeu a menor codificação, podendo ser representado por apenas 1 bit "1". Os símbolos com menor frequência, C:2 e X:4, receberam uma codificação maior, precisando de 4 bits para representá-los.

Finalmente, eu criei uma função chamada ```gen-bit-code``` que vai gerar a codificação para cada nó folha dessa árvore.

{% highlight clojure %}
(defn is-leaf? [node]
  (and (nil? (:left node))
       (nil? (:right node))))

(defn gen-bitcode [htree
                   code
                   hcodes]
  (if (is-leaf? htree)
    (conj hcodes {:code code :sym (:symbol htree)})
    (let [left (:left htree)
          right (:right htree)]
      (concat
       (gen-bitcode left
                    (str code "0")
                    hcodes)
       (gen-bitcode right
                    (str code "1")
                    hcodes)))))

{% endhighlight %}

O resultado final em Clojure foi o seguinte:
{% highlight clojure %}
({:code "00", :sym "e"} {:code "010", :sym "j"} {:code "0110", :sym "c"} {:code "0111", :sym "x"} {:code "1", :sym "a"})
{% endhighlight %}

Eu gerei um vetor de códigos, mas poderia melhorar e retornar um hash-map cuja chave seria o símbolo e o valor seria o bit-code em string mas está bom por hoje.

## Conclusão

Nesse post nós aprendemos como funciona a *Codificação de Huffman* para a compactação de textos. Em um outro post, sabe-se lá quando, vou explicar como gerar o bitstream do texto compactado usando um array de bytes e manipulação de bits. Para finalmente salvarmos em um arquivo compactado. Um abraço e até lá.

# Referências

[1] - [https://www.gta.ufrj.br/grad/06_2/alexandre/criptoanalise.html](https://www.gta.ufrj.br/grad/06_2/alexandre/criptoanalise.html)